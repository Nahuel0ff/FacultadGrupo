programa ejercicio1

procesos
  proceso juntarFlores(ES flores: numero; ES esquinasSinFlores: numero)
  comenzar
    si(HayFlorEnLaEsquina)
      mientras(HayFlorEnLaEsquina)
        tomarFlor
        flores := flores + 1
    sino
      esquinasSinFlores := esquinasSinFlores + 1
  fin

  proceso caminarAvenida(ES flores: numero; ES esquinasSinFlores: numero)
  comenzar
    Pos(1, 1)
    repetir 99
      juntarFlores(flores, esquinasSinFlores)
      mover
    juntarFlores(flores, esquinasSinFlores)
    Informar(flores)
    Informar(esquinasSinFlores)
  fin

  proceso depositarFlores(E flores: numero)
  comenzar
    Pos(1, 100)
    repetir flores
      depositarFlor
  fin

areas
  ciudad: AreaC(1, 1, 100, 100)

robots
  robot robot1
  variables
    flores:numero 
    esquinasSinFlores: numero
  comenzar
    flores := 0
    esquinasSinFlores := 0
    caminarAvenida(flores, esquinasSinFlores)
    depositarFlores(flores)
  fin

variables
  R_info: robot1

comenzar
  AsignarArea(R_info, ciudad)
  Iniciar(R_info, 1, 1)
fin
